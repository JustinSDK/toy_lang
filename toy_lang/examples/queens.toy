from '/lib/math' import abs

def queenss(n) {
    def placeQueens(k) {
       if k == 0 {
           return [[]]
       }

       def collect(queens) {
           addQs = (acc, qs) -> acc.concat([qs])
           (return range(1, n + 1).filter(column -> isSafe([k, column], queens))
                                  .map(column -> queens.concat([[k, column]]))
                                  .reduce(addQs, []))
       }       

       addQueens = (acc, queens) -> acc.concat(collect(queens))
       return placeQueens(k - 1).reduce(addQueens, [])
    }

    return placeQueens(n)
}

def isSafe(queen, queens) {
    return queens.all(q -> not inCheck(queen, q))
}

def inCheck(q1, q2) {
    (return q1.get(0) == q2.get(0) or 
            q1.get(1) == q2.get(1) or 
            abs(q1.get(0) - q2.get(0)) == abs(q1.get(1) - q2.get(1))) 
}

def printQS(qs) {
    qs.forEach(print)
    println()
}

queenss(8).forEach(printQS)